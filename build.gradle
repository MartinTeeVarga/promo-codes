buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'promocodes'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release'
    }
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.93'

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-web'

    compile "org.webjars:angularjs:1.4.3"
    compile "org.webjars:jquery:2.1.1"
    compile "org.webjars:bootstrap:3.2.0"
    compile "org.webjars:webjars-locator:+"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.security.oauth:spring-security-oauth2"

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'com.amazonaws:DynamoDBLocal:1.11.0.1'
    testCompile 'org.assertj:assertj-core:3.6.1'

}

test {
    jacoco {
        excludes = [
                "com.czequered.promocodes.config.*",
                "com.czequered.promocodes.service.*Dev",
                "com.czequered.promocodes.PromoCodesApp"
        ]
    }
    testLogging {
        showStandardStreams = true
    }
    doFirst {
        def libs = configurations.testCompile
                .filter { it.name.endsWith('.dylib') || it.name.endsWith('.so') || it.name.endsWith('.dll') }
                .collect { it.parent }
                .join(File.pathSeparator)
        systemProperty "java.library.path", "$libs"
    }
}

bootRun {
    systemProperty "spring.profiles.active", "prod"
}

task bootRunDev(type: org.springframework.boot.gradle.run.BootRunTask, group: "application") {
    main = "com.czequered.promocodes.PromoCodesApp"
    classpath = sourceSets.main.runtimeClasspath
    systemProperty "spring.profiles.active", "dev"
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(it).matching {
                exclude 'com/czequered/promocodes/config/**', 'com/czequered/promocodes/**/*Dev.*', 'com/czequered/promocodes/PromoCodesApp.*'
            }
        })
    }
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}